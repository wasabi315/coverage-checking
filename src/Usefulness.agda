module Usefulness where

open import Data.Fin as Fin using (zero; suc)
open import Data.Fin.Subset using (Subset; _âˆ‰_; âŠ¥; â…_â†; _âˆª_; âˆ; â‹ƒ; Nonempty)
open import Data.Fin.Subset.Properties using (xâˆ‰â…yâ†â‡’xâ‰¢y; xâˆˆâˆpâ‡’xâˆ‰p; nonempty?)
open import Data.Fin.Properties using (any?)
open import Data.List as List using (List; []; _âˆ·_; _++_; concatMap)
open import Data.List.Relation.Unary.All as All using (All; []; _âˆ·_)
import Data.List.Relation.Unary.All.Properties as All
open import Data.List.Relation.Unary.First as First using (First)
open import Data.List.Relation.Unary.First.Properties as First using (Allâ‡’Â¬First)
open import Data.Nat using (â„•; zero; suc)
open import Data.Product as Product using (âˆƒ-syntax; _Ã—_; _,_)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Function using (id; _âˆ˜_; _â‡”_; mkâ‡”)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; â‰¢-sym)
open import Relation.Nullary.Decidable as Dec using (Dec; yes; no; _âŠ-dec_)
open import Relation.Nullary.Negation using (Â¬_; contradiction; contraposition)

open import Extra
open import Pattern

private
  variable
    Î± Î² : Ty
    Î±s Î²s : List Ty

--------------------------------------------------------------------------------
-- Exhaustiveness and usefulness

-- There is a matching row in pss for every list of values
Exhaustive : PatMat Î±s â†’ Set
Exhaustive pss = âˆ€ vs â†’ Match vs pss

-- There is a list of values that does not match any row in pss
NonExhaustive : PatMat Î±s â†’ Set
NonExhaustive pss = âˆƒ[ vs ] Â¬ Match vs pss

-- ps is useful with respect to pss if
--   1. there is a list of values that matches ps (say vs)
--   2. vs does not match any row in pss
-- Usefulness can also be used to formulate redundancy
Useful : Pats Î±s â†’ PatMat Î±s â†’ Set
Useful ps pss = âˆƒ[ vs ] ps â‰¼* vs Ã— All (_â‹ * vs) pss

-- non-exhaustiveness defined in terms of usefulness:
-- pss is non-exhaustive if âˆ™* is useful with respect to pss
NonExhaustiveâ€² : PatMat Î±s â†’ Set
NonExhaustiveâ€² = Useful âˆ™*

-- pss is exhaustive if âˆ™* is not useful with respect to pss
Exhaustiveâ€² : PatMat Î±s â†’ Set
Exhaustiveâ€² pss = Â¬ NonExhaustiveâ€² pss

module _ {pss : PatMat Î±s} where

  NonExhaustiveâ€²â†’NonExhaustive : NonExhaustiveâ€² pss â†’ NonExhaustive pss
  NonExhaustiveâ€²â†’NonExhaustive (vs , _ , âˆ™*psâ‹ vs) = vs , Allâ‡’Â¬First id âˆ™*psâ‹ vs

  NonExhaustiveâ†’NonExhaustiveâ€² : NonExhaustive pss â†’ NonExhaustiveâ€² pss
  NonExhaustiveâ†’NonExhaustiveâ€² (vs , pssâ‹ vs) = vs , âˆ™*â‰¼ , Â¬Firstâ‡’All id pssâ‹ vs

  -- The two definitions of non-exhaustiveness are equivalent
  NonExhaustiveâ€²â‡”NonExhaustive : NonExhaustiveâ€² pss â‡” NonExhaustive pss
  NonExhaustiveâ€²â‡”NonExhaustive = mkâ‡” NonExhaustiveâ€²â†’NonExhaustive NonExhaustiveâ†’NonExhaustiveâ€²

  Exhaustiveâ†’Exhaustiveâ€² : Exhaustive pss â†’ Exhaustiveâ€² pss
  Exhaustiveâ†’Exhaustiveâ€² exh (vs , _ , pssâ‹ vs) = Allâ‡’Â¬First id pssâ‹ vs (exh vs)

  Exhaustiveâ€²â†’Exhaustive : Exhaustiveâ€² pss â†’ Exhaustive pss
  Exhaustiveâ€²â†’Exhaustive exh vs with match? vs pss
  ... | yes pssâ‰¼vs = pssâ‰¼vs
  ... | no pssâ‹ vs = contradiction (vs , âˆ™*â‰¼ , Â¬Firstâ‡’All id pssâ‹ vs) exh

  -- The two definitions of exhaustiveness are equivalent
  Exhaustiveâ€²â‡”Exhaustive : Exhaustiveâ€² pss â‡” Exhaustive pss
  Exhaustiveâ€²â‡”Exhaustive = mkâ‡” Exhaustiveâ€²â†’Exhaustive Exhaustiveâ†’Exhaustiveâ€²

--------------------------------------------------------------------------------

-- Set of root constructors of a pattern
rootCons : Pat Î± â†’ ConSet Î±
rootCons âˆ™ = âŠ¥
rootCons (con c _) = â… c â†
rootCons (p âˆ£ q) = rootCons p âˆª rootCons q

-- Set of root constructors in the first column of a pattern matrix
Î£ : PatMat (Î± âˆ· Î±s) â†’ ConSet Î±
Î£ = â‹ƒ âˆ˜ List.map (rootCons âˆ˜ All.head)

-- Specialization: filters out clauses whose first pattern does not match a value of the form `con c -`.
ğ’®-aux : âˆ€ c â†’ Pats (Î± âˆ· Î±s) â†’ List (Pats (args Î± c ++ Î±s))
ğ’®-aux c (âˆ™ âˆ· ps) = All.++âº âˆ™* ps âˆ· []
ğ’®-aux c (con d rs âˆ· ps) with c Fin.â‰Ÿ d
... | no _ = []
... | yes refl = All.++âº rs ps âˆ· []
ğ’®-aux c (râ‚ âˆ£ râ‚‚ âˆ· ps) = ğ’®-aux c (râ‚ âˆ· ps) ++ ğ’®-aux c (râ‚‚ âˆ· ps)

ğ’® : âˆ€ c â†’ PatMat (Î± âˆ· Î±s) â†’ PatMat (args Î± c ++ Î±s)
ğ’® = concatMap âˆ˜ ğ’®-aux

-- Default matrix: filters out clauses whose first pattern is a constructor pattern
ğ’Ÿ-aux : Pats (Î± âˆ· Î±s) â†’ List (Pats Î±s)
ğ’Ÿ-aux (âˆ™ âˆ· ps) = ps âˆ· []
ğ’Ÿ-aux (con _ _ âˆ· ps) = []
ğ’Ÿ-aux (râ‚ âˆ£ râ‚‚ âˆ· ps) = ğ’Ÿ-aux (râ‚ âˆ· ps) ++ ğ’Ÿ-aux (râ‚‚ âˆ· ps)

ğ’Ÿ : PatMat (Î± âˆ· Î±s) â†’ PatMat Î±s
ğ’Ÿ = concatMap ğ’Ÿ-aux

--------------------------------------------------------------------------------

-- [] is useful wrt []
useful-[]-[] : Useful [] []
useful-[]-[] = [] , [] , []

-- [] is not wrt any non-empty matrix
Â¬useful-[]-âˆ· : âˆ€ {ps pss} â†’ Â¬ Useful [] (ps âˆ· pss)
Â¬useful-[]-âˆ· {ps = []} ([] , _ , Â¬[] âˆ· _) = Â¬[] []

module _ {râ‚ râ‚‚ : Pat Î±} {ps : Pats Î±s} {pss} where

  useful-âˆ£âº : Useful (râ‚ âˆ· ps) pss âŠ Useful (râ‚‚ âˆ· ps) pss â†’ Useful (râ‚ âˆ£ râ‚‚ âˆ· ps) pss
  useful-âˆ£âº (injâ‚ (vvs , râ‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs)) =
    vvs , âˆ£â‰¼Ë¡ râ‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs
  useful-âˆ£âº (injâ‚‚ (vvs , râ‚‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs)) =
    vvs , âˆ£â‰¼Ê³ râ‚‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs

  useful-âˆ£â» : Useful (râ‚ âˆ£ râ‚‚ âˆ· ps) pss â†’ Useful (râ‚ âˆ· ps) pss âŠ Useful (râ‚‚ âˆ· ps) pss
  useful-âˆ£â» (vvs , âˆ£â‰¼Ë¡ râ‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs) =
    injâ‚ (vvs , râ‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs)
  useful-âˆ£â» (vvs , âˆ£â‰¼Ê³ râ‚‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs) =
    injâ‚‚ (vvs , râ‚‚â‰¼v âˆ· psâ‰¼vs , pssâ‹ vvs)

  -- (râ‚ âˆ£ râ‚‚ âˆ· ps) is useful wrt pss iff (râ‚ âˆ· ps) or (râ‚‚ âˆ· ps) is useful wrt pss
  useful-âˆ£â‡” : (Useful (râ‚ âˆ· ps) pss âŠ Useful (râ‚‚ âˆ· ps) pss) â‡” Useful (râ‚ âˆ£ râ‚‚ âˆ· ps) pss
  useful-âˆ£â‡” = mkâ‡” useful-âˆ£âº useful-âˆ£â»


module _ {c} {us : Vals (args Î± c)} {vs : Vals Î±s} where

  ğ’®-aux-pres-â‹  : âˆ€ {ps}
    â†’ ps â‹ * con {Î±} c us âˆ· vs
    â†’ All (_â‹ * All.++âº us vs) (ğ’®-aux c ps)
  ğ’®-aux-pres-â‹  {âˆ™ âˆ· ps} âˆ™psâ‹ cusvs = contraposition âˆ™â‰¼*âº âˆ™psâ‹ cusvs âˆ· []
  ğ’®-aux-pres-â‹  {con d rs âˆ· ps} drspsâ‹ cusvs with c Fin.â‰Ÿ d
  ... | no _ = []
  ... | yes refl = contraposition conâ‰¼*âº drspsâ‹ cusvs âˆ· []
  ğ’®-aux-pres-â‹  {râ‚ âˆ£ râ‚‚ âˆ· ps} râ‚â‚‚psâ‹ cusvs =
    All.++âº
      (ğ’®-aux-pres-â‹  (contraposition (âˆ£â‰¼*âº âˆ˜ injâ‚) râ‚â‚‚psâ‹ cusvs))
      (ğ’®-aux-pres-â‹  (contraposition (âˆ£â‰¼*âº âˆ˜ injâ‚‚) râ‚â‚‚psâ‹ cusvs))

  -- ğ’® preserves â‹ : if all rows in `pss` do not match `con c us âˆ· vs`, all rows in the specialized matrix do not match `ğ’® c (con c us âˆ· vs) = us ++ vs`.
  ğ’®-pres-â‹  : âˆ€ {pss}
    â†’ All (_â‹ * con c us âˆ· vs) pss
    â†’ All (_â‹ * All.++âº us vs) (ğ’® c pss)
  ğ’®-pres-â‹  = All.concatâº âˆ˜ All.gmapâº ğ’®-aux-pres-â‹ 

  ğ’®-aux-pres-â‹ â» : âˆ€ {ps}
    â†’ All (_â‹ * All.++âº us vs) (ğ’®-aux c ps)
    â†’ ps â‹ * con {Î±} c us âˆ· vs
  ğ’®-aux-pres-â‹ â» {âˆ™ âˆ· ps} (âˆ™*psâ‹ usvs âˆ· []) (âˆ™â‰¼ âˆ· psâ‰¼vs) =
    âˆ™*psâ‹ usvs (++âº âˆ™*â‰¼ psâ‰¼vs)
  ğ’®-aux-pres-â‹ â» {con c rs âˆ· ps} ğ’®rspsâ‹ usvs (conâ‰¼ rsâ‰¼us âˆ· psâ‰¼vs) with c Fin.â‰Ÿ c | â‰Ÿ-refl c
  ğ’®-aux-pres-â‹ â» {con c rs âˆ· ps} (rspsâ‹ usvs âˆ· []) (conâ‰¼ rsâ‰¼us âˆ· psâ‰¼vs) | _ | refl =
    rspsâ‹ usvs (++âº rsâ‰¼us psâ‰¼vs)
  ğ’®-aux-pres-â‹ â» {râ‚ âˆ£ râ‚‚ âˆ· ps} ğ’®râ‚psğ’®râ‚‚psâ‹ usvs (âˆ£â‰¼Ë¡ râ‚â‰¼cus âˆ· psâ‰¼vs) =
    ğ’®-aux-pres-â‹ â» (All.++â»Ë¡ _ ğ’®râ‚psğ’®râ‚‚psâ‹ usvs) (râ‚â‰¼cus âˆ· psâ‰¼vs)
  ğ’®-aux-pres-â‹ â» {râ‚ âˆ£ râ‚‚ âˆ· ps} ğ’®râ‚psğ’®râ‚‚psâ‹ usvs (âˆ£â‰¼Ê³ râ‚‚â‰¼cus âˆ· psâ‰¼vs) =
    ğ’®-aux-pres-â‹ â» (All.++â»Ê³ _ ğ’®râ‚psğ’®râ‚‚psâ‹ usvs) (râ‚‚â‰¼cus âˆ· psâ‰¼vs)

  -- The inverse of ğ’®-pres-â‹ : if all rows in the specialized matrix do not match `us ++ vs`, all rows the "unspecialized" matrix do not match `con c us âˆ· vs` (unspecialized values).
  ğ’®-pres-â‹ â» : âˆ€ {pss}
    â†’ All (_â‹ * All.++âº us vs) (ğ’® c pss)
    â†’ All (_â‹ * con c us âˆ· vs) pss
  ğ’®-pres-â‹ â» = All.gmapâ» ğ’®-aux-pres-â‹ â» âˆ˜ All.concatâ»

  -- Specialization and unspecialization preserve â‹ 
  ğ’®-pres-â‹ â‡” : âˆ€ {pss}
    â†’ All (_â‹ * All.++âº us vs) (ğ’® c pss) â‡” All (_â‹ * con c us âˆ· vs) pss
  ğ’®-pres-â‹ â‡” = mkâ‡” ğ’®-pres-â‹ â» ğ’®-pres-â‹ 


module _ {c} {rs : Pats (args Î± c)} {ps : Pats Î±s} {pss : PatMat (Î± âˆ· Î±s)} where

  useful-conâº : Useful (All.++âº rs ps) (ğ’® c pss) â†’ Useful (con c rs âˆ· ps) pss
  useful-conâº (usvs , rspsâ‰¼usvs , ğ’®pssâ‹ usvs)
    with us , vs , refl , rsâ‰¼us , psâ‰¼vs â† split rs rspsâ‰¼usvs =
    con c us âˆ· vs , conâ‰¼ rsâ‰¼us âˆ· psâ‰¼vs , ğ’®-pres-â‹ â» ğ’®pssâ‹ usvs

  useful-conâ» : Useful (con c rs âˆ· ps) pss â†’ Useful (All.++âº rs ps) (ğ’® c pss)
  useful-conâ» (con c vs âˆ· us , conâ‰¼ rsâ‰¼vs âˆ· psâ‰¼us , pssâ‹ cvsus) =
    All.++âº vs us , ++âº rsâ‰¼vs psâ‰¼us , ğ’®-pres-â‹  pssâ‹ cvsus

  -- con c rs âˆ· ps is useful wrt pss iff rs ++ ps is useful wrt ğ’® c pss
  useful-conâ‡” : Useful (All.++âº rs ps) (ğ’® c pss) â‡” Useful (con c rs âˆ· ps) pss
  useful-conâ‡” = mkâ‡” useful-conâº useful-conâ»


module _ {Î± Î±s} {ps : Pats Î±s} {pss} where

  -- If there exists a constructor c such that `âˆ™* ++ ps` is useful wrt `ğ’® c pss`, `âˆ™ âˆ· ps` is also useful wrt pss
  useful-âˆ™-ğ’®âº : âˆƒ[ c ] Useful (All.++âº âˆ™* ps) (ğ’® c pss) â†’ Useful (âˆ™ {Î±} âˆ· ps) pss
  useful-âˆ™-ğ’®âº (c , usvs , âˆ™*psâ‰¼usvs , ğ’®pssâ‹ usvs)
    with us , vs , refl , _ , psâ‰¼vs â† split {args Î± c} âˆ™* âˆ™*psâ‰¼usvs =
    con c us âˆ· vs , âˆ™â‰¼ âˆ· psâ‰¼vs , ğ’®-pres-â‹ â» ğ’®pssâ‹ usvs

  -- If `âˆ™ âˆ· ps` is useful wrt pss, there exists a constructor c such that `âˆ™* ++ ps` is useful wrt `ğ’® c pss`
  useful-âˆ™-ğ’®â» : Useful (âˆ™ {Î±} âˆ· ps) pss â†’ âˆƒ[ c ] Useful (All.++âº âˆ™* ps) (ğ’® c pss)
  useful-âˆ™-ğ’®â» (con c us âˆ· vs , âˆ™â‰¼ âˆ· psâ‰¼vs , pssâ‹ cusvs) =
    c , All.++âº us vs , ++âº âˆ™*â‰¼ psâ‰¼vs , ğ’®-pres-â‹  pssâ‹ cusvs

  -- âˆ™ âˆ· ps is useful wrt pss iff âˆ™* ++ ps is useful wrt ğ’® c pss
  useful-âˆ™-ğ’®â‡” : (âˆƒ[ c ] Useful (All.++âº âˆ™* ps) (ğ’® c pss)) â‡” Useful (âˆ™ {Î±} âˆ· ps) pss
  useful-âˆ™-ğ’®â‡” = mkâ‡” useful-âˆ™-ğ’®âº useful-âˆ™-ğ’®â»

module _ {v : Val Î±} {vs : Vals Î±s} where

  ğ’Ÿ-aux-pres-â‹  : âˆ€ {ps} â†’ ps â‹ * v âˆ· vs â†’ All (_â‹ * vs) (ğ’Ÿ-aux ps)
  ğ’Ÿ-aux-pres-â‹  {âˆ™ âˆ· ps} âˆ™psâ‹ vvs =
    contraposition (âˆ™â‰¼ âˆ·_) âˆ™psâ‹ vvs âˆ· []
  ğ’Ÿ-aux-pres-â‹  {con _ _ âˆ· ps} _ = []
  ğ’Ÿ-aux-pres-â‹  {râ‚ âˆ£ râ‚‚ âˆ· ps} râ‚â‚‚psâ‹ vvs =
    All.++âº
      (ğ’Ÿ-aux-pres-â‹  (contraposition (âˆ£â‰¼*âº âˆ˜ injâ‚) râ‚â‚‚psâ‹ vvs))
      (ğ’Ÿ-aux-pres-â‹  (contraposition (âˆ£â‰¼*âº âˆ˜ injâ‚‚) râ‚â‚‚psâ‹ vvs))

  -- ğ’Ÿ preserves â‹ : if all rows in `pss` do not match `v âˆ· vs`, all rows in the default matrix do not match `vs`
  ğ’Ÿ-pres-â‹  : âˆ€ {pss} â†’ All (_â‹ * v âˆ· vs) pss â†’ All (_â‹ * vs) (ğ’Ÿ pss)
  ğ’Ÿ-pres-â‹  = All.concatâº âˆ˜ All.gmapâº ğ’Ÿ-aux-pres-â‹ 


module _ {c} {us : Vals (args Î± c)} {vs : Vals Î±s} where

  câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» : âˆ€ {ps}
    â†’ c âˆ‰ rootCons (All.head ps)
    â†’ All (_â‹ * vs) (ğ’Ÿ-aux ps)
    â†’ ps â‹ * con {Î±} c us âˆ· vs
  câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» {âˆ™ âˆ· ps} câˆ‰ps (psâ‹ vs âˆ· []) (âˆ™â‰¼ âˆ· psâ‰¼vs) = psâ‹ vs psâ‰¼vs
  câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» {con d _ âˆ· ps} câˆ‰â…dâ† [] (dâ‰¼c âˆ· psâ‰¼vs) =
    câ‰¢dâ†’câ‹ d (â‰¢-sym (xâˆ‰â…yâ†â‡’xâ‰¢y câˆ‰â…dâ†)) dâ‰¼c
  câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» {râ‚ âˆ£ râ‚‚ âˆ· ps} câˆ‰râ‚âˆªrâ‚‚ ğ’Ÿrâ‚psğ’Ÿrâ‚‚psâ‹ vs (âˆ£â‰¼Ë¡ râ‚â‰¼cus âˆ· psâ‰¼vs) =
    câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» (xâˆ‰pâˆªqâ»Ë¡ câˆ‰râ‚âˆªrâ‚‚) (All.++â»Ë¡ _  ğ’Ÿrâ‚psğ’Ÿrâ‚‚psâ‹ vs) (râ‚â‰¼cus âˆ· psâ‰¼vs)
  câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» {râ‚ âˆ£ râ‚‚ âˆ· ps} câˆ‰râ‚âˆªrâ‚‚ ğ’Ÿrâ‚psğ’Ÿrâ‚‚psâ‹ vs (âˆ£â‰¼Ê³ râ‚‚â‰¼cus âˆ· psâ‰¼vs) =
    câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» (xâˆ‰pâˆªqâ»Ê³ câˆ‰râ‚âˆªrâ‚‚) (All.++â»Ê³ _  ğ’Ÿrâ‚psğ’Ÿrâ‚‚psâ‹ vs) (râ‚‚â‰¼cus âˆ· psâ‰¼vs)

  -- If c is not one of the root constructors of the first pattern in ps, and all rows in the default matrix do not match `vs`, all rows in the original matrix do not match `con c us âˆ· vs`
  câˆ‰pssâ†’ğ’Ÿ-pres-â‹ â» : âˆ€ {pss}
    â†’ c âˆ‰ Î£ pss
    â†’ All (_â‹ * vs) (ğ’Ÿ pss)
    â†’ All (_â‹ * con {Î±} c us âˆ· vs) pss
  câˆ‰pssâ†’ğ’Ÿ-pres-â‹ â» {[]} câˆ‰pss [] = []
  câˆ‰pssâ†’ğ’Ÿ-pres-â‹ â» {ps âˆ· pss} câˆ‰psâˆªpss ğ’Ÿpsğ’Ÿpssâ‹ vs =
    let ğ’Ÿpsâ‹ vs , ğ’Ÿpssâ‹ vs = All.++â» (ğ’Ÿ-aux ps) ğ’Ÿpsğ’Ÿpssâ‹ vs
        câˆ‰ps , câˆ‰pss = xâˆ‰pâˆªqâ» câˆ‰psâˆªpss
     in câˆ‰psâ†’ğ’Ÿ-aux-pres-â‹ â» câˆ‰ps ğ’Ÿpsâ‹ vs âˆ· câˆ‰pssâ†’ğ’Ÿ-pres-â‹ â» câˆ‰pss ğ’Ÿpssâ‹ vs


module _ {Î±} {ps : Pats Î±s} {pss} where

  -- If Î£ pss is not complete, and ps is useful wrt ğ’Ÿ pss, âˆ™ âˆ· ps is also useful wrt pss.
  -- That means, it suffices to check for usefulness of ps wrt ğ’Ÿ pss if Î£ pss is not complete.
  useful-âˆ™-ğ’Ÿâº :
      Nonempty (âˆ (Î£ pss))
    â†’ Useful ps (ğ’Ÿ pss)
    â†’ Useful (âˆ™ {Î±} âˆ· ps) pss
  useful-âˆ™-ğ’Ÿâº (c , câˆˆâˆpss) (vs , psâ‰¼vs , ğ’Ÿpssâ‹ vs) =
    inhabOf c âˆ· vs , âˆ™â‰¼ âˆ· psâ‰¼vs , câˆ‰pssâ†’ğ’Ÿ-pres-â‹ â» (xâˆˆâˆpâ‡’xâˆ‰p câˆˆâˆpss) ğ’Ÿpssâ‹ vs

  -- ps is useful wrt (ğ’Ÿ pss) if (âˆ™ âˆ· ps) is useful wrt pss
  useful-âˆ™-ğ’Ÿâ» : Useful (âˆ™ {Î±} âˆ· ps) pss â†’ Useful ps (ğ’Ÿ pss)
  useful-âˆ™-ğ’Ÿâ» (v âˆ· vs , âˆ™â‰¼ âˆ· psâ‰¼vs , pssâ‹ vvs) = vs , psâ‰¼vs , ğ’Ÿ-pres-â‹  pssâ‹ vvs

--------------------------------------------------------------------------------
-- Usefulness checking algorithm

{-# TERMINATING #-}
useful? : (ps : Pats Î±s) (pss : PatMat Î±s) â†’ Dec (Useful ps pss)
useful? [] [] = yes useful-[]-[]
useful? [] (_ âˆ· _) = no Â¬useful-[]-âˆ·
useful? (âˆ™ âˆ· ps) pss with nonempty? (âˆ (Î£ pss))
... | yes âˆƒcâˆˆâˆpss =
      Dec.mapâ€² (useful-âˆ™-ğ’Ÿâº âˆƒcâˆˆâˆpss) useful-âˆ™-ğ’Ÿâ» (useful? ps (ğ’Ÿ pss))
... | no _ =
      Dec.map useful-âˆ™-ğ’®â‡” (any? Î» c â†’ useful? (All.++âº âˆ™* ps) (ğ’® c pss))
useful? (con c rs âˆ· ps) pss =
  Dec.map useful-conâ‡” (useful? (All.++âº rs ps) (ğ’® c pss))
useful? (râ‚ âˆ£ râ‚‚ âˆ· ps) pss =
  Dec.map useful-âˆ£â‡” (useful? (râ‚ âˆ· ps) pss âŠ-dec useful? (râ‚‚ âˆ· ps) pss)

exhaustive? : (pss : PatMat Î±s) â†’ Exhaustive pss âŠ NonExhaustive pss
exhaustive? pss with useful? âˆ™* pss
... | yes h = injâ‚‚ (NonExhaustiveâ€²â†’NonExhaustive h)
... | no h = injâ‚ (Exhaustiveâ€²â†’Exhaustive h)
